# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: apps/casm

import os

Import('env', 'casm_lib', 'ccasm_lib', 'build_lib_paths')

# Compile casm.cpp

# include paths
candidates = [
  env['INCDIR'], 
  env.include_path(env['BOOST_INCLUDEDIR'], 'boost')
]
cpppath = [x for x in candidates if x is not None]

casm_obj = env.Object('casm.cpp', 
                      CPPPATH = cpppath)
Default(casm_obj)
Export('casm_obj')
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [casm_obj]

# Construct 'casm' executable

# link flags
linkflags = ""
if env['PLATFORM'] == 'darwin':
  linkflags = ['-rpath', '@loader_path/../lib']
else:
  linkflags = ['-Wl,--rpath=\\$$ORIGIN/../lib', '-z', 'origin']

casm = env.Program(os.path.join(env['BINDIR'], 'casm'),
                   [casm_obj],
                   CPPPATH=cpppath,
                   LIBPATH=build_lib_paths,
                   LINKFLAGS=linkflags,
                   LIBS=[
                     env['boost_system'], 
                     env['boost_filesystem'], 
                     env['boost_program_options'], 
                     env['boost_regex'], 
                     env['boost_chrono'], 
                     env['dl'], 
                     env['z'],
                     'casm'])
                   
Depends(casm, [casm_lib, ccasm_lib])
Default(casm)
env.Alias('casm', casm)
Export('casm')
env['COMPILE_TARGETS'] = env['COMPILE_TARGETS'] + [casm]

# Install instructions
casm_install = env.Install(env['CASM_BINDIR'], casm)
env.Alias('casm_install', casm_install)
Export('casm_install')
env['INSTALL_TARGETS'] = env['INSTALL_TARGETS'] + [casm_install]

if 'casm_install' in COMMAND_LINE_TARGETS:
    env['IS_INSTALL'] = 1
