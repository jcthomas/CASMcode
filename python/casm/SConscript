# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: python/casm

import os
from distutils.spawn import find_executable

Import('env', 'version_number', 'url')

pycasm_setup = env.Command('setup.py', ['make_setup.py'], 'cd python/casm && python make_setup.py ' + version_number + ' ' + url)
env.NoClean(pycasm_setup)

cmd = 'cd python/casm && ' + find_executable('python') + ' setup.py install'
cmd += ' --prefix=' + env['CASM_PYTHON_PREFIX']
cmd += ' --record installed.txt'

pycasm_install = env.Command('installed.txt', [pycasm_setup], cmd)
AlwaysBuild(pycasm_install)
env.Append(INSTALL_TARGETS = [pycasm_install])
Alias('pycasm_install', pycasm_install)  

# with setup.py, must uninstall files manually, 
#  so we store a file with the install location of the files
installed_files = []
if os.path.isfile('installed.txt'):
  installed_files = [line.rstrip('\n') for line in open('installed.txt')]

env.Clean(pycasm_install, installed_files + ['build', 'dist', 'casm.egg-info'])

if 'pycasm_install' in COMMAND_LINE_TARGETS:
  env['IS_INSTALL'] = 1
