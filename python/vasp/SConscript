# http://www.scons.org/doc/production/HTML/scons-user.html
# This is: python/vasp

import os
from distutils.spawn import find_executable

Import('env', 'version_number', 'url')

pyvasp_setup = env.Command('setup.py', ['make_setup.py'], 'cd python/vasp && python make_setup.py ' + version_number + ' ' + url)
env.NoClean(pyvasp_setup)

cmd = 'cd python/vasp && ' + find_executable('python') + ' setup.py install'
cmd += ' --prefix=' + env['CASM_PYTHON_PREFIX']
cmd += ' --record installed.txt'

pyvasp_install = env.Command('installed.txt', [pyvasp_setup], cmd)
AlwaysBuild(pyvasp_install)
env.Append(INSTALL_TARGETS = [pyvasp_install])
Alias('pyvasp_install', pyvasp_install)  

# with setup.py, must uninstall files manually, 
#  so we store a file with the install location of the files
installed_files = []
if os.path.isfile('installed.txt'):
  installed_files = [line.rstrip('\n') for line in open('installed.txt')]

env.Clean(pyvasp_install, installed_files + ['build', 'dist', 'vasp.egg-info'])

if 'pyvasp_install' in COMMAND_LINE_TARGETS:
  env['IS_INSTALL'] = 1

    
